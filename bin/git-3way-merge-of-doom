#!/usr/bin/perl

# Copyright (C) 2010   Keith Amling, keith.amling@gmail.com
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

$| = 1;

use strict;
use warnings;

use FindBin;
use lib "$FindBin::RealBin/../libs";

use Amling::Git::G3MD::Parser;
use Amling::Git::G3MD::Resolver::Git;
use Amling::Git::G3MD::Resolver;
use Amling::Git::Utils;
use Getopt::Long;

my $index_files = 0;
my $index_read = 0;
my $index_write = 0;

my @options =
(
    'index-files!' => \$index_files,
    'index-read!' => \$index_read,
    'index-write!' => \$index_write,
    'index!' => sub { $index_files = 1; $index_read = 1; $index_write = 1; },
    'help' => sub { usage(0); },
);

GetOptions(@options) || usage(1);

my @f = @ARGV;
@ARGV = ();

my $index_data;
{
    my $index_data_cached;
    $index_data = sub
    {
        if(!defined($index_data_cached))
        {
            $index_data_cached = {};
            open(my $fh, '-|', 'git', 'ls-files', '-u') || die "Could not open git ls-files -u: $!";
            while(my $l = <$fh>)
            {
                chomp $l;
                if($l =~ /^\d+ [0-9a-f]{40} ([123])\t(.*)$/)
                {
                    ($index_data_cached->{$2} ||= {})->{$1} = 1;
                }
            }
            close($fh) || die "Could not open git ls-files -u: $!";
        }
        return $index_data_cached;
    };
}

if($index_files)
{
    push @f, sort(keys(%{$index_data->()}));
}

for my $f (@f)
{
    my $blocks;
    if($index_read)
    {
        my $stages = $index_data->()->{$f};
        if(!defined($stages))
        {
            die "$f is not conflicted in the index!";
        }
        my $stage_contents = sub
        {
            my $stage = shift;
            if($stages->{$stage})
            {
                return Amling::Git::Utils::show(":$stage:./$f"),
            }
            else
            {
                return [];
            }
        };
        $blocks =
        [
            [
                'CONFLICT',
                "$f:LHS",
                $stage_contents->(2),
                "$f:MHS",
                $stage_contents->(1),
                "$f:RHS",
                $stage_contents->(3),
            ],
        ];
        $blocks = Amling::Git::G3MD::Resolver::Git->resolve_blocks($blocks);
    }
    else
    {
        my $lines = Amling::Git::Utils::slurp($f);
        $blocks = Amling::Git::G3MD::Parser::parse_3way($lines);
    }
    my $lines2 = Amling::Git::G3MD::Resolver::resolve_blocks($blocks);
    Amling::Git::Utils::unslurp($f, $lines2);
    if($index_write)
    {
        my $blocks2 = Amling::Git::G3MD::Parser::parse_3way($lines2);
        my $conflict = 0;
        for my $block (@$blocks2)
        {
            my $type = $block->[0];

            if(0)
            {
            }
            elsif($type eq 'LINE')
            {
                next;
            }
            elsif($type eq 'CONFLICT')
            {
                $conflict = 1;
            }
            else
            {
                die;
            }
        }
        if($conflict)
        {
            print "$f: still has conflicts, not adding to git index!\n";
        }
        else
        {
            (system('git', 'add', $f) == 0) || die "Could not system git add $f: $!";
            print "$f: successfully resolved and added to git index.\n";
        }
    }
}

sub usage
{
    my $ec = shift;

    print <<EOF;
Usage: git-3way-merge-of-doom <options> [<files>]
   --index-files                 Operate on all conflicted files in the git
                                 index.
   --index-read                  Read file contents out of conflicted git index
                                 instead of file contents in working tree.
   --index-write                 Add files to git index if resolution looks
                                 clean (no conflict blocks remain).
   --index                       Enables all --index-* options.
   --help                        Print this help.

Options to resolve at the prompt are:
EOF
    print map { "   $_\n" } @{Amling::Git::G3MD::Resolver->all_help()};

    exit $ec;
}
